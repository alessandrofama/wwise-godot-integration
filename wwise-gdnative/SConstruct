#!python
import os, sys, subprocess

if sys.version_info < (3,):
    def decode_utf8(x):
        return x
else:
    import codecs
    def decode_utf8(x):
        return codecs.utf_8_decode(x)[0]

# Workaround for MinGW. See:
# http://www.scons.org/wiki/LongCmdLinesOnWin32
if os.name == "nt":
    import subprocess

    def mySubProcess(cmdline,env):
        #print "SPAWNED : " + cmdline
        startupinfo = subprocess.STARTUPINFO()
        startupinfo.dwFlags |= subprocess.STARTF_USESHOWWINDOW
        proc = subprocess.Popen(cmdline, stdin=subprocess.PIPE, stdout=subprocess.PIPE,
            stderr=subprocess.PIPE, startupinfo=startupinfo, shell = False, env = env)
        data, err = proc.communicate()
        rv = proc.wait()
        if rv:
            print("=====")
            print(err.decode("utf-8"))
            print("=====")
        return rv

    def mySpawn(sh, escape, cmd, args, env):

        newargs = ' '.join(args[1:])
        cmdline = cmd + " " + newargs

        rv=0
        if len(cmdline) > 32000 and cmd.endswith("ar") :
            cmdline = cmd + " " + args[1] + " " + args[2] + " "
            for i in range(3,len(args)) :
                rv = mySubProcess( cmdline + args[i], env )
                if rv :
                    break
        else:
            rv = mySubProcess( cmdline, env )

        return rv

def sys_exec(args):
    proc = subprocess.Popen(args, stdout=subprocess.PIPE, text=True)
    (out, err) = proc.communicate()
    return out.rstrip("\r\n").lstrip()

# Try to detect the host platform automatically.
# This is used if no `platform` argument is passed
if sys.platform.startswith('linux'):
    host_platform = 'linux'
elif sys.platform == 'darwin':
    host_platform = 'osx'
elif sys.platform == 'win32' or sys.platform == 'msys':
    host_platform = 'windows'
else:
    raise ValueError(
        'Could not detect platform automatically, please specify with '
        'platform=<platform>'
    )

opts = Variables([], ARGUMENTS)

# Gets the standard flags CC, CCX, etc.
env = DefaultEnvironment()

# Define our options
opts.Add(EnumVariable('target', "Compilation target", 'debug', ['d', 'debug', 'r', 'release']))
opts.Add(EnumVariable('platform', "Compilation platform", '', ['', 'windows', 'x11', 'linux', 'osx', 'ios', 'android']))
opts.Add(EnumVariable('p', "Compilation target, alias for 'platform'", '', ['', 'windows', 'x11', 'linux', 'osx']))
opts.Add(BoolVariable('use_llvm', "Use the LLVM / Clang compiler", 'no'))
opts.Add(PathVariable('target_path', 'The path where the lib is installed.', 'gdnative-demo/wwise/bin/'))
opts.Add(PathVariable('target_name', 'The library name.', 'WwiseGDNative', PathVariable.PathAccept))
opts.Add(PathVariable('wwise_sdk', 'The Wwise SDK path', '', PathVariable.PathAccept))
opts.Add(ListVariable('plugins', 'List of plugins', '', ['reflect', 'motion', 'convolution', 'soundseed_grain', 'soundseed_air_impact']))
opts.Add(EnumVariable('ios_arch', 'Target iOS architecture', 'arm64', ['armv7', 'arm64']))
opts.Add('IPHONEPATH', 'Path to iPhone toolchain', '/Applications/Xcode.app/Contents/Developer/Toolchains/XcodeDefault.xctoolchain')
opts.Add(EnumVariable('android_arch', 'Target Android architecture', 'armv7', ['armv7','arm64v8','x86','x86_64']))
opts.Add('android_api_level', 'Target Android API level','17' if ARGUMENTS.get("android_arch", 'armv7') in ['armv7', 'x86'] else '21')
opts.Add('ANDROID_NDK_ROOT', 'Path to your Android NDK installation. By default, uses ANDROID_NDK_ROOT from your defined environment variables.', os.environ.get("ANDROID_NDK_ROOT", None))

# Local dependency paths, adapt them to your setup
godot_headers_path = "../godot-cpp/godot_headers/"
cpp_bindings_path = "../godot-cpp/"
cpp_library = "libgodot-cpp"

# only support 64 at this time..
bits = 64

# Updates the environment with the option variables.
opts.Update(env)

# Wwise dependency paths
wwise_sdk_headers_path = env['wwise_sdk'] + "/include/"
wwise_sdk_libs_path = ""
wwise_soundengine_sample_path = ""

if env['platform'] == "windows":
    wwise_soundengine_sample_path = env['wwise_sdk'] + "/samples/SoundEngine/Win32/"
    if env['target'] in ('debug', 'd'):
        wwise_sdk_libs_path = env['wwise_sdk'] + "/x64_vc150/Debug/lib/"
    else:
        wwise_sdk_libs_path = env['wwise_sdk'] + "/x64_vc150/Release/lib/"

if env['platform'] == "osx":
    wwise_soundengine_sample_path = env['wwise_sdk'] + "/samples/SoundEngine/POSIX/"
    if env['target'] in ('debug', 'd'):
        wwise_sdk_libs_path = env['wwise_sdk'] + "/Mac/Debug/lib/"
    else:
        wwise_sdk_libs_path = env['wwise_sdk'] + "/Mac/Release/lib/"   

if env['platform'] == "ios":
    wwise_soundengine_sample_path = env['wwise_sdk'] + "/samples/SoundEngine/POSIX/"
    if env['target'] in ('debug', 'd'):
        wwise_sdk_libs_path = env['wwise_sdk'] + "/iOS/Debug-iphoneos/lib/"
    else:
        wwise_sdk_libs_path = env['wwise_sdk'] + "/iOS/Release-iphoneos/lib/"   

if env['platform'] == "android":
    wwise_soundengine_sample_path = env['wwise_sdk'] + "/samples/SoundEngine/Android/"
    if env['target'] in ('debug', 'd'):
        if env['android_arch'] == 'armv7':
            wwise_sdk_libs_path = env['wwise_sdk'] + "/Android_armeabi-v7a/Debug/lib/"
        elif env['android_arch'] == 'arm64v8':
            wwise_sdk_libs_path = env['wwise_sdk'] + "/Android_arm64-v8a/Debug/lib/"
        elif env['android_arch'] == 'x86':
            wwise_sdk_libs_path = env['wwise_sdk'] + "/Android_x86/Debug/lib/"
        elif env['android_arch'] == 'x86_64':
            wwise_sdk_libs_path = env['wwise_sdk'] + "/Android_x86_64/Debug/lib/"
    else:
        if env['android_arch'] == 'armv7':
            wwise_sdk_libs_path = env['wwise_sdk'] + "/Android_armeabi-v7a/Release/lib/"
        elif env['android_arch'] == 'arm64v8':
            wwise_sdk_libs_path = env['wwise_sdk'] + "/Android_arm64-v8a/Release/lib/"
        elif env['android_arch'] == 'x86':
            wwise_sdk_libs_path = env['wwise_sdk'] + "/Android_x86/Release/lib/"
        elif env['android_arch'] == 'x86_64':
            wwise_sdk_libs_path = env['wwise_sdk'] + "/Android_x86_64/Release/lib/"

wwise_soundengine_sample_common_path = env['wwise_sdk'] + "/samples/SoundEngine/Common/"
wwise_memorymanager_library = "AkMemoryMgr"
wwise_soundengine_library = "AkSoundEngine"
wwise_musicengine_library = "AkMusicEngine"
wwise_streammanager_library = "AkStreamMgr"
wwise_communications_library = "CommunicationCentral"
wwise_plugins_library = ["AkGainFX", "AkHarmonizerFX", "AkStereoDelayFX", "AkTimeStretchFX",
                         "AkTremoloFX", "AkGuitarDistortionFX","AkVorbisDecoder", "AkMeterFX", 
                         "AkPitchShifterFX", "AkRoomVerbFX", "AkRoomVerbFX", "AkPeakLimiterFX", 
                         "AkExpanderFX", "AkCompressorFX", "AkMatrixReverbFX", "AkParametricEQFX", 
                         "AkDelayFX","AkSilenceSource", "AkToneSource", "AkFlangerFX", "AkSineSource",
                         "AkRecorderFX", "AkAudioInputSource", "AkSynthOneSource", "AkSpatialAudio"]

if 'reflect' in env['plugins']:
    wwise_plugins_library.append('AkReflectFX')

if 'convolution' in env['plugins']:
    wwise_plugins_library.append('AkConvolutionReverbFX')

if 'soundseed_grain' in env['plugins']:
    wwise_plugins_library.append('AkSoundSeedGrainSource')

if 'soundseed_air_impact' in env['plugins']:
    wwise_plugins_library.append(['AkSoundSeedWindSource', 'AkSoundSeedImpactFX', 'AkSoundSeedWooshSource'])

print("Wwise SDK headers path: " + wwise_sdk_headers_path)
print("Wwise SDK libs path: " + wwise_sdk_libs_path)

# Process some arguments
if env['use_llvm']:
    env['CC'] = 'clang'
    env['CXX'] = 'clang++'

if env['p'] != '':
    env['platform'] = env['p']

if env['platform'] == '':
    print("No valid target platform selected.")
    quit();

# For the reference:
# - CCFLAGS are compilation flags shared between C and C++
# - CFLAGS are for C-specific compilation flags
# - CXXFLAGS are for C++-specific compilation flags
# - CPPFLAGS are for pre-processor flags
# - CPPDEFINES are for pre-processor defines
# - LINKFLAGS are for linking flags

# Check our platform specifics
if env['platform'] == "osx":
    env['target_path'] += 'osx/'
    if env['target'] in ('debug', 'd'):
        env['target_path'] += 'debug/'
    else:
        env['target_path'] += 'release/'

    cpp_library += '.osx'

    wwise_plugins_library.append("AkAACDecoder")
    
    if env['target'] in ('debug', 'd'):
        env.Append(CCFLAGS=['-g', '-O2', '-arch', 'x86_64'])
        env.Append(CXXFLAGS=['-std=c++17'])
        env.Append(LINKFLAGS=['-arch', 'x86_64', '-framework', 'CoreAudio', '-Wl,-undefined,dynamic_lookup'])
    else:
        env.Append(CPPDEFINES=['AK_OPTIMIZED'])
        env.Append(CCFLAGS=['-g', '-O3', '-arch', 'x86_64'])
        env.Append(CXXFLAGS=['-std=c++17'])
        env.Append(LINKFLAGS=['-arch', 'x86_64', '-framework', 'CoreAudio', '-Wl,-undefined,dynamic_lookup'])

    if 'reflect' in env['plugins']:
        env.Append(CPPDEFINES=['AK_REFLECT'])
    if 'convolution' in env['plugins']:
        env.Append(CPPDEFINES=['AK_CONVOLUTION'])
    if 'soundseed_grain' in env['plugins']:
        env.Append(CPPDEFINES=['AK_SOUNDSEED_GRAIN'])
    if 'soundseed_air_impact' in env['plugins']:
        env.Append(CPPDEFINES=['AK_SOUNDSEED_AIR_IMPACT'])

elif env['platform'] == 'ios':

    env['target_path'] += 'iOS/'
    if env['target'] in ('debug', 'd'):
        env['target_path'] += 'debug/' + env['ios_arch'] + "/"
    elif env['target'] in ('release', 'r'):
        env['target_path'] += 'release/' + env['ios_arch'] + "/"
    
    cpp_library += '.ios'

    sdk_name = 'iphoneos'
    env.Append(CCFLAGS=['-miphoneos-version-min=10.0'])

    try:
        sdk_path = decode_utf8(subprocess.check_output(['xcrun', '--sdk', sdk_name, '--show-sdk-path']).strip())
    except (subprocess.CalledProcessError, OSError):
        raise ValueError("Failed to find SDK path while running xcrun --sdk {} --show-sdk-path.".format(sdk_name))

    compiler_path = env['IPHONEPATH'] + '/usr/bin/'
    env['ENV']['PATH'] = env['IPHONEPATH'] + "/Developer/usr/bin/:" + env['ENV']['PATH']

    env['CC'] = compiler_path + 'clang'
    env['CXX'] = compiler_path + 'clang++'
    env['AR'] = compiler_path + 'ar'
    env['RANLIB'] = compiler_path + 'ranlib'

    if env['target'] == 'debug':
        env.Append(CCFLAGS=['-Og', '-g', '-std=c++14', '-arch', env['ios_arch'], '-isysroot', sdk_path])
        env.Append(LINKFLAGS=['-framework', 'CoreAudio', '-framework', 'AudioToolbox', '-arch', env['ios_arch'], 
                            '-Wl,-undefined,dynamic_lookup', '-isysroot', sdk_path, '-F' + sdk_path])
    elif env['target'] == 'release':
        env.Append(CPPDEFINES=['AK_OPTIMIZED'])
        env.Append(CCFLAGS=['-O3', '-g', '-std=c++14', '-arch', env['ios_arch'], '-isysroot', sdk_path])
        env.Append(LINKFLAGS=['-framework', 'CoreAudio', '-framework', 'AudioToolbox', '-arch', env['ios_arch'], 
                            '-Wl,-undefined,dynamic_lookup', '-isysroot', sdk_path, '-F' + sdk_path])

elif env['platform'] == 'android':

    env['target_path'] += 'android/'
    if env['target'] in ('debug', 'd'):
        env['target_path'] += 'debug/' + env['android_arch'] + "/"
    elif env['target'] in ('release', 'r'):
        env['target_path'] += 'release/' + env['android_arch'] + "/"

    cpp_library += '.android'

    if host_platform == 'windows':
        env = env.Clone(tools=['mingw'])
        env["SPAWN"] = mySpawn

    # Verify NDK root
    if not 'ANDROID_NDK_ROOT' in env:
        raise ValueError("To build for Android, ANDROID_NDK_ROOT must be defined. Please set ANDROID_NDK_ROOT to the root folder of your Android NDK installation.")

    # Validate API level
    api_level = int(env['android_api_level'])
    if env['android_arch'] in ['x86_64', 'arm64v8'] and api_level < 21:
        print("WARN: 64-bit Android architectures require an API level of at least 21; setting android_api_level=21")
        env['android_api_level'] = '21'
        api_level = 21

    # Setup toolchain
    toolchain = env['ANDROID_NDK_ROOT'] + "/toolchains/llvm/prebuilt/"
    if host_platform == "windows":
        toolchain += "windows"
        import platform as pltfm
        if pltfm.machine().endswith("64"):
            toolchain += "-x86_64"
    elif host_platform == "linux":
        toolchain += "linux-x86_64"
    elif host_platform == "osx":
        toolchain += "darwin-x86_64"
    env.PrependENVPath('PATH', toolchain + "/bin") # This does nothing half of the time, but we'll put it here anyways

    # Get architecture info
    arch_info_table = {
        "armv7" : {
            "march":"armv7-a", "target":"armv7a-linux-androideabi", "tool_path":"arm-linux-androideabi", "compiler_path":"armv7a-linux-androideabi",
            "ccflags" : ['-mfpu=neon'], "target_platform": "arch-arm"
            },
        "arm64v8" : {
            "march":"armv8-a", "target":"aarch64-linux-android", "tool_path":"aarch64-linux-android", "compiler_path":"aarch64-linux-android",
            "ccflags" : [], "target_platform": "arch-arm64"
            },
        "x86" : {
            "march":"i686", "target":"i686-linux-android", "tool_path":"i686-linux-android", "compiler_path":"i686-linux-android",
            "ccflags" : ['-mstackrealign'], "target_platform": "arch-x86"
            },
        "x86_64" : {"march":"x86-64", "target":"x86_64-linux-android", "tool_path":"x86_64-linux-android", "compiler_path":"x86_64-linux-android",
            "ccflags" : [], "target_platform": "arch-x86_64"
        }
    }
    arch_info = arch_info_table[env['android_arch']]

    env['CC'] = toolchain + "/bin/clang"
    env['CXX'] = toolchain + "/bin/clang++"
    env['AR'] = toolchain + "/bin/" + arch_info['tool_path'] + "-ar"
    env["AS"] = toolchain + "/bin/" + arch_info['tool_path'] + "-as"
    env["LD"] = toolchain + "/bin/" + arch_info['tool_path'] + "-ld"
    env["STRIP"] = toolchain + "/bin/" + arch_info['tool_path'] + "-strip"
    env["RANLIB"] = toolchain + "/bin/" + arch_info['tool_path'] + "-ranlib"
    env['OBJCOPY'] = toolchain + "/bin/" + arch_info['tool_path'] + "-objcopy"
    env['LINK'] = toolchain + "/bin/clang++"
    target_platform = env['ANDROID_NDK_ROOT'] + ("/platforms/android-%s" % api_level) + ('/%s/usr/lib' % arch_info['target_platform'])
    env['SHLINKFLAGS'] = ["-Wl", "-shared", "--sysroot=%s" % target_platform, "-Wl", "-z", "noexecstack"]

    env.Append(CCFLAGS=['--target=' + arch_info['target'] + env['android_api_level'], '-march=' + arch_info['march'], '-fPIC'])
    env.Append(CCFLAGS= arch_info['ccflags'])

    if env['target'] == 'debug':
        env.Append(CCFLAGS=['-std=c++14'])
        env.Append(LINKFLAGS=['-Wl,-undefined,dynamic_lookup'])
        env.Append(LIBS=['android', 'OpenSLES', 'zip', 'z'])
    elif env['target'] == 'release':
        env.Append(CPPDEFINES=['AK_OPTIMIZED'])
        env.Append(CCFLAGS=['-std=c++14'])
        env.Append(LINKFLAGS=['-Wl,-undefined,dynamic_lookup'])
        env.Append(LIBS=['android', 'OpenSLES', 'zip', 'z'])

elif env['platform'] in ('x11', 'linux'):
    env['target_path'] += 'x11/'
    cpp_library += '.linux'
    if env['target'] in ('debug', 'd'):
        env.Append(CCFLAGS=['-fPIC', '-g3', '-Og'])
        env.Append(CXXFLAGS=['-std=c++17'])
    else:
        env.Append(CCFLAGS=['-fPIC', '-g', '-O3'])
        env.Append(CXXFLAGS=['-std=c++17'])

elif env['platform'] == "windows":
    env['target_path'] += 'win64/'
    if env['target'] in ('debug', 'd'):
        env['target_path'] += 'debug/'
    else:
        env['target_path'] += 'release/'

    cpp_library += '.windows'

    if 'motion' in env['plugins']:
        wwise_plugins_library.append(['AkMotionSink', 'AkMotionSourceSource'])
    
    # This makes sure to keep the session environment variables on windows,
    # that way you can run scons in a vs 2017 prompt and it will find all the required tools
    env.Append(ENV=os.environ)

    env.Append(CPPDEFINES=['WIN32', '_WIN32', '_WINDOWS', '_CRT_SECURE_NO_WARNINGS', 'UNICODE'])
    env.Append(CCFLAGS=['-W3', '-GR'])
    #env.Append(CCFLAGS=['/GS', '/GL', '/Gy'])

    if env['target'] in ('debug', 'd'):
        env.Append(CPPDEFINES=['_DEBUG'])
        env.Append(CCFLAGS=['-EHsc', '-MDd', '-ZI'])
        env.Append(LINKFLAGS=['-DEBUG'])
    else:
        env.Append(CPPDEFINES=['AK_OPTIMIZED'])
        env.Append(CPPDEFINES=['NDEBUG'])
        env.Append(CCFLAGS=['-O2', '-EHsc', '-MD'])

    if 'reflect' in env['plugins']:
        env.Append(CPPDEFINES=['AK_REFLECT'])
    if 'motion' in env['plugins']:
        env.Append(CPPDEFINES=['AK_MOTION'])
    if 'convolution' in env['plugins']:
        env.Append(CPPDEFINES=['AK_CONVOLUTION'])
    if 'soundseed_grain' in env['plugins']:
        env.Append(CPPDEFINES=['AK_SOUNDSEED_GRAIN'])
    if 'soundseed_air_impact' in env['plugins']:
        env.Append(CPPDEFINES=['AK_SOUNDSEED_AIR_IMPACT'])

if env['target'] in ('debug', 'd'):
    cpp_library += '.debug'
else:
    cpp_library += '.release'

if env['platform'] == 'ios':
    if env['ios_arch'] == 'arm64':
        cpp_library += '.' + 'arm64'
    elif env['ios_arch'] == 'armv7':
        cpp_library += '.' + 'armv7'
elif env['platform'] == 'android':
    if env['android_arch'] == 'armv7':
        cpp_library += '.' + 'armv7'
    elif env['android_arch'] == 'arm64v8':
        cpp_library += '.' + 'armv64v8'
    elif env['android_arch'] == 'x86':
        cpp_library += '.' + 'x86'
    elif env['android_arch'] == 'x86_64':
        cpp_library += '.' + 'x86_64'
else:
    cpp_library += '.' + str(bits)

# make sure our binding library is properly includes
env.Append(CPPPATH=['.', godot_headers_path, cpp_bindings_path + 'include/', cpp_bindings_path + 'include/core/', cpp_bindings_path + 'include/gen/'])
env.Append(LIBPATH=[cpp_bindings_path + 'bin/'])
env.Append(LIBS=[cpp_library])

# tweak this if you want to use different folders, or more folders, to store your source code in.
env.Append(CPPPATH=['src/'])

sources = []
sources.append(Glob('src/*.cpp')) 
sources.append(Glob(wwise_soundengine_sample_path + '*.cpp'))
sources.append(Glob(wwise_soundengine_sample_common_path + '*.cpp'))

# wwise specific configuration
env.Append(CPPPATH=[wwise_sdk_headers_path, wwise_soundengine_sample_path])
env.Append(LIBPATH=[wwise_sdk_libs_path])
env.Append(LIBS=[wwise_memorymanager_library, wwise_soundengine_library, wwise_streammanager_library, wwise_musicengine_library, wwise_plugins_library])

if env['target'] in ('debug', 'd'):
    env.Append(LIBS=[wwise_communications_library])

    if env['platform'] == "windows":
    	env.Append(LIBS=['msacm32', 'ws2_32'])

if env['platform'] == "windows":
    env.Append(LIBS=['advapi32', 'user32', 'ole32'])
if env['platform'] == "windows" and 'motion' in env['plugins']:
    env.Append(LIBS=['XInput', 'Dinput8', 'Winmm', 'oleaut32'])

library = env.SharedLibrary(target=env['target_path'] + env['target_name'] , source=sources)

Default(library)

# Generates help for the -h scons option.
Help(opts.GenerateHelpText(env))
